---
title: 深度围观block：第二集
author: BeyondVincent
layout: post

simplecatch-sidebarlayout:
  - 
duoshuo_thread_id:
  - 1248139211572248696
ludou_ratings_score:
  - 3
categories:
  - iOS
  - iOS翻译
tags:
  - block
---
[<img class="alignnone size-full wp-image-1571" alt="blocks" src="http://beyondvincent.com/wp-content/uploads/2013/07/blocks.png" width="561" height="233" />][1]

本文由**<span style="color: #339966;">破船</span>**译自<span style="text-decoration: underline;"><strong><span style="color: #339966;"><a href="http://www.galloway.me.uk/2012/10/a-look-inside-blocks-episode-2/" target="_blank"><span style="color: #339966; text-decoration: underline;">galloway</span></a></span></strong></span>转载请注明出处！

## **<span style="color: #339966;">小引</span>**

今天翻译了第二篇，这个翻译是比较痛苦(其实不止这篇，所有的都是)， 不比单纯的阅读，许多地方需要查阅资料，并细心的遣词造句，还得注意词不达意的地方(例如文中的**<span style="color: #339966;">A block that captures scope</span>**我翻译为**<span style="color: #339966;">block的拷贝范围</span>**，总感觉缺了一些作者原意，功力有限啊)。所以，我劝大家要是能看原文尽量去看原文吧，我这翻译的权当参考。
<!--more-->

## **<span style="color: #339966;">目录</span>**

*   **<span style="color: #339966;">介绍</span>**
*   **<span style="color: #339966;">block类型</span>**
*   **<span style="color: #339966;">block的拷贝范围</span>**
*   **<span style="color: #339966;">block拷贝对象的类型</span>**
*   **<span style="color: #339966;">何去何从</span>**

### **<span style="color: #339966;">正文</span>**

## **<span style="color: #339966;">介绍</span>**

本文接着上一篇文章(**<span style="text-decoration: underline;"><span style="color: #339966;"><a href="http://beyondvincent.com/2013/07/09/%E6%B7%B1%E5%BA%A6%E5%9B%B4%E8%A7%82block%EF%BC%9A%E7%AC%AC%E4%B8%80%E9%9B%86/" target="_blank"><span style="color: #339966; text-decoration: underline;">深度围观block：第一集</span></a></span></span>**)，继续从编译器的角度深度围观block。在本文中，将介绍block并不是一成不变的，以及block在栈上的构成。

## **<span style="color: #339966;">block类型</span>**

在**<span style="text-decoration: underline;"><span style="color: #339966; text-decoration: underline;"><a href="http://beyondvincent.com/2013/07/09/%E6%B7%B1%E5%BA%A6%E5%9B%B4%E8%A7%82block%EF%BC%9A%E7%AC%AC%E4%B8%80%E9%9B%86/" target="_blank"><span style="color: #339966; text-decoration: underline;">第一篇</span></a></span></span>**文章中，我们已经看到block有一个**<span style="color: #339966;">_NSConcreteGlobalBlock</span>**这样的类。由于所有变量都是已知的，所以在编译期间，block的结构(structure)和描述(descriptor)都将全部被初始化。关于block这里有几种不同的类型，每种类型都有对应的类。为了简单起见，这里只考虑其中三种：

1.  **<span style="color: #339966;">_NSConcreteGlobalBlock</span>**是定义一个全局的block，在编译器就已经完成相关初始化任务。这种类型的block不会涉及到任何拷贝，例如一个空的block。
2.  **<span style="color: #339966;">_NSConcreteStackBlock</span>**是一个分配在栈上的block。这里是所有最终被拷贝到堆(heap)上的block的开始。
3.  **<span style="color: #339966;">_NSConcreteMallocBlock</span>**是分配到堆(heap)上的block。拷贝完一个block之后，这就会结束。当block的引用计数变为0，该block就会被释放。

## **<span style="color: #339966;">block拷贝范围</span>**

这次我们来看看另外一些代码，如下所示：

<pre class="wp-code-highlight prettyprint linenums:1">#import &lt;dispatch/dispatch.h&gt;

typedef void(^BlockA)(void);
void foo(int);

__attribute__((noinline))
void runBlockA(BlockA block) {
    block();
}

void doBlockA() {
    int a = 128;
    BlockA block = ^{
        foo(a);
    };
    runBlockA(block);
}</pre>

为了让block拷贝一些内容，上面的代码中调用了**<span style="color: #339966;">foo</span>**函数，并给这个函数传递了一个变量。再说一下，本文涉及到的汇编代码是与armv7相关指令。下面是其中一部分汇编指令：

<pre class="wp-code-highlight prettyprint linenums:1">.globl  _runBlockA
    .align  2
    .code   16                      @ @runBlockA
    .thumb_func     _runBlockA
_runBlockA:
    ldr     r1, [r0, #12]
    bx      r1</pre>

上面的汇编代码与**<span style="color: #339966;">runBlockA</span>**函数相关，这跟第一篇文章中的相同——都是调用了block中的**<span style="color: #339966;">invoke</span>**函数。接着是**<span style="color: #339966;">doBlockA</span>**汇编代码，如下所示：

<pre class="wp-code-highlight prettyprint linenums:1">.globl  _doBlockA
    .align  2
    .code   16                      @ @doBlockA
    .thumb_func     _doBlockA
_doBlockA:
    push    {r7, lr}
    mov     r7, sp
    sub     sp, #24
    movw    r2, :lower16:(L__NSConcreteStackBlock$non_lazy_ptr-(LPC1_0+4))
    movt    r2, :upper16:(L__NSConcreteStackBlock$non_lazy_ptr-(LPC1_0+4))
    movw    r1, :lower16:(___doBlockA_block_invoke_0-(LPC1_1+4))
LPC1_0:
    add     r2, pc
    movt    r1, :upper16:(___doBlockA_block_invoke_0-(LPC1_1+4))
    movw    r0, :lower16:(___block_descriptor_tmp-(LPC1_2+4))
LPC1_1:
    add     r1, pc
    ldr     r2, [r2]
    movt    r0, :upper16:(___block_descriptor_tmp-(LPC1_2+4))
    str     r2, [sp]
    mov.w   r2, #1073741824
    str     r2, [sp, #4]
    movs    r2, #0
LPC1_2:
    add     r0, pc
    str     r2, [sp, #8]
    str     r1, [sp, #12]
    str     r0, [sp, #16]
    movs    r0, #128
    str     r0, [sp, #20]
    mov     r0, sp
    bl      _runBlockA
    add     sp, #24
    pop     {r7, pc}</pre>

看看，这跟之前的代码有所不同了。看起来这不仅仅是从一个全局的符号中加载block，而且还做了额外的一些事情。乍一看这么多代码让人有点无从下手，不过认真看，还是很容易理解的。从上面的代码可以看出，编译器已经忽略了对代码排序的优化，为了方便阅读代码，我对上面的汇编代码重新进行排序(当然，请相信我，这不会影响任何功能)。下面是我重排好的代码效果：

<pre class="wp-code-highlight prettyprint linenums:1">_doBlockA:
        // 1
        push    {r7, lr}
        mov     r7, sp

        // 2
        sub     sp, #24

        // 3
        movw    r2, :lower16:(L__NSConcreteStackBlock$non_lazy_ptr-(LPC1_0+4))
        movt    r2, :upper16:(L__NSConcreteStackBlock$non_lazy_ptr-(LPC1_0+4))
LPC1_0:
        add     r2, pc
        ldr     r2, [r2]
        str     r2, [sp]

        // 4
        mov.w   r2, #1073741824
        str     r2, [sp, #4]

        // 5
        movs    r2, #0
        str     r2, [sp, #8]

        // 6
        movw    r1, :lower16:(___doBlockA_block_invoke_0-(LPC1_1+4))
        movt    r1, :upper16:(___doBlockA_block_invoke_0-(LPC1_1+4))
LPC1_1:
        add     r1, pc
        str     r1, [sp, #12]

        // 7
        movw    r0, :lower16:(___block_descriptor_tmp-(LPC1_2+4))
        movt    r0, :upper16:(___block_descriptor_tmp-(LPC1_2+4))
LPC1_2:
        add     r0, pc
        str     r0, [sp, #16]

        // 8
        movs    r0, #128
        str     r0, [sp, #20]

        // 9
        mov     r0, sp
        bl      _runBlockA

        // 10
        add     sp, #24
        pop     {r7, pc}</pre>

下面我们来看看这些代码都做了什么：

1.  开场白。首先将 **<span style="color: #339966;">r7</span>** push到栈上面——因为r7会被覆盖，而r7寄存器中的内容在跨函数调用时是需要用到的。**<span style="color: #339966;">lr</span>**是链接寄存器(link register)，该寄存器中存储着当这个函数返回时需要执行下一条指令的地址。接着mov这条指令的作用是把栈指针保存到r7**<span style="color: #339966;">寄存器</span>**中。
2.  从栈指针所处位置开始减去24，也就是在栈空间上开辟24字节来存储数据。
3.  这里涉及到的代码是为了对符号**<span style="color: #339966;">L__NSConcreteStackBlock$non_lazy_ptr</span>**进行寻址，由于跟pc(program counter)相关联，所以无论代码处于二进制文件中任何位置，当最终链接时，都能对该符号做到正确的寻址。
4.  将值**<span style="color: #339966;">1073741824</span>**存储到栈指针 + 4 的位置。
5.  将值**<span style="color: #339966;"></span>**存储到栈指针 + 8 的位置。现在，将要发生什么可能已经变得逐渐清晰了——在栈上创建了一个**<span style="color: #339966;">Block_layout</span>**结构的对象！到现在为止，已经设置了该结构的3个值：**<span style="color: #339966;">isa</span>**指针，**<span style="color: #339966;">flags</span>**和**<span style="color: #339966;">reserved</span>**值。
6.  将**<span style="color: #339966;">___doBlockA_block_invoke_0</span>**存储至栈指针 + 12的位置。这是block结构中的**<span style="color: #339966;">invoke</span>**。
7.  将**<span style="color: #339966;">___block_descriptor_tmp</span>**存储至栈指针 + 16的位置。这是block结构中的**<span style="color: #339966;">descriptor</span>**。
8.  将值**<span style="color: #339966;">128</span>**存储到栈指针 + 20的位置。如果回头看看**<span style="color: #339966;">Block_layout</span>**结构，可以看到里面只应该有5个值。那么在这个block结构体后面存储的128是什么呢？——注意到这个**<span style="color: #339966;">128</span>**实际上就是在block中拷贝的变量的值。所以这肯定就是存储block使用到的值的地方——在**<span style="color: #339966;">Block_layout</span>**结构尾部。
9.  现在栈指针指向了已经完成初始化之后的block结构，在这里的汇编指令是将栈指针装载到**<span style="color: #339966;">r0</span>**中，然后调用**<span style="color: #339966;">runBlockA</span>**函数。(记住：在ARM EABI中，**<span style="color: #339966;">r0</span>**中存储的内容被当做函数的第一个参数)。
10. 最后将栈指针加上24，这样就能够把最开始减去的24(在栈上开辟的24位空间)收回来。接着将栈中的两个值pop到**<span style="color: #339966;">r7</span>**和**<span style="color: #339966;">pc</span>**寄存器中。这里pop到**<span style="color: #339966;">r7</span>**中的，跟最开始从r7中push至栈中的内容是一致的，而**<span style="color: #339966;">pc</span>**的值则是最开始push **<span style="color: #339966;">lr</span>**到栈中的值，这样当函数返回时，可以让CPU能够正确的继续执行后续指令。

Cooool！如果你一直认真看到这里，那么相信你的收获已经非常多了！

下面我们再看看block中的invoke函数和descriptor。希望跟第一集中的不要有太大差别。如下汇编代码：

<pre class="wp-code-highlight prettyprint linenums:1">.align  2
    .code   16                      @ @__doBlockA_block_invoke_0
    .thumb_func     ___doBlockA_block_invoke_0
___doBlockA_block_invoke_0:
    ldr     r0, [r0, #20]
    b.w     _foo

    .section        __TEXT,__cstring,cstring_literals
L_.str:                                 @ @.str
    .asciz   "v4@?0"

    .section        __TEXT,__objc_classname,cstring_literals
L_OBJC_CLASS_NAME_:                     @ @"\01L_OBJC_CLASS_NAME_"
    .asciz   "\001P"

    .section        __DATA,__const
    .align  2                       @ @__block_descriptor_tmp
___block_descriptor_tmp:
    .long   0                       @ 0x0
    .long   24                      @ 0x18
    .long   L_.str
    .long   L_OBJC_CLASS_NAME_</pre>

看着没错，跟第一集中的没多大区别。唯一不同的就是block descriptor中的**<span style="color: #339966;">size</span>**——现在是**<span style="color: #339966;">24</span>**(之前是**<span style="color: #339966;">20</span>**)。这是因为block拷贝了一个整型值，所以block的结构需要24个字节，而不再是标准的20个字节了。在之前的代码中，我们已经分析了在创建block时，多出的4个字节被添加到block结构的尾部。  
在实际的block函数中，例如**<span style="color: #339966;">___doBlockA_block_invoke_0</span>**，可以看到从block结构尾部读取出相关值，如**<span style="color: #339966;">r0 + 20</span>**，就是在block中拷贝的变量。

## **<span style="color: #339966;">block拷贝对象的类型</span>**

下面我们来看看如果block拷贝的是别的对象类型(例如 **<span style="color: #339966;">NSString</span>**)，而不是integer，会发生什么呢？如下代码：

<pre class="wp-code-highlight prettyprint linenums:1">#import &lt;dispatch/dispatch.h&gt;

typedef void(^BlockA)(void);
void foo(NSString*);

__attribute__((noinline))
void runBlockA(BlockA block) {
    block();
}

void doBlockA() {
    NSString *a = @"A";
    BlockA block = ^{
        foo(a);
    };
    runBlockA(block);
}</pre>

由于**<span style="color: #339966;">doBlockA</span>**变化不大，所以在此不深入介绍。这里感兴趣的是根据上面代码创建的block descriptor结构：

<pre class="wp-code-highlight prettyprint linenums:1">.section        __DATA,__const
    .align  4                       @ @__block_descriptor_tmp
___block_descriptor_tmp:
    .long   0                       @ 0x0
    .long   24                      @ 0x18
    .long   ___copy_helper_block_
    .long   ___destroy_helper_block_
    .long   L_.str1
    .long   L_OBJC_CLASS_NAME_</pre>

注意看上面的汇编代码中有指向两个函数(**<span style="color: #339966;">___copy_helper_block_</span>**和**<span style="color: #339966;">___destroy_helper_block_</span>**)的指针。下面是这两个函数的定义：

<pre class="wp-code-highlight prettyprint linenums:1">.align  2
    .code   16                      @ @__copy_helper_block_
    .thumb_func     ___copy_helper_block_
___copy_helper_block_:
    ldr     r1, [r1, #20]
    adds    r0, #20
    movs    r2, #3
    b.w     __Block_object_assign

    .align  2
    .code   16                      @ @__destroy_helper_block_
    .thumb_func     ___destroy_helper_block_
___destroy_helper_block_:
    ldr     r0, [r0, #20]
    movs    r1, #3
    b.w     __Block_object_dispose</pre>

这里我先假设当block被拷贝和销毁时，都会调用这里的函数。那么被block拷贝的对象肯定会发生reatain和release。上面的代码中，可以看出如果r0和r1包含有效数据时，拷贝函数接收两个参数(**<span style="color: #339966;">r0</span>**和**<span style="color: #339966;">r1</span>**)。而销毁函数接收一个参数。可以看出所有的拷贝和销毁任务都应该是由**<span style="color: #339966;">__Block_object_assign</span>**和**<span style="color: #339966;">__Block_object_dispose</span>**两个函数完成的。这两个函数位于block的运行时代码中(是LLVM里面**<span style="color: #339966;">compiler-rt</span>**工程的一部分)。  
如果你希望了解一下block运行时相关代码，可以来这里下载源码：**<span style="text-decoration: underline;"><span style="color: #339966; text-decoration: underline;"><a href="http://compiler-rt.llvm.org" target="_blank"><span style="color: #339966; text-decoration: underline;">http://compiler-rt.llvm.org</span></a></span></span>**。特别关注一下里面的**<span style="color: #339966;">runtime.c</span>**文件。

## **<span style="color: #339966;">何去何从</span>**

在下一集中我将调查**<span style="color: #339966;">Block_copy</span>**相关代码，并看看相关工作处理情况，以此来深度围观一下block运行时。通过下一集的学习，你也将会深入了解拷贝和销毁函数(也就是本文中我们刚刚看到的在block拷贝对象时使用的函数)。

本文由<span style="text-decoration: underline;"><span style="color: #339966;"><strong><a href="http://beyondvincent.com/"><span style="color: #339966; text-decoration: underline;">破船</span></a></strong></span></span>翻译●转载请注明出处●<time datetime="2013-05-18T17:37:00+08:00" data-updated="true">2013-07-10</time>

 [1]: http://beyondvincent.com/wp-content/uploads/2013/07/blocks.png